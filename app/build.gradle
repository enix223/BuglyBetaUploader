buildscript {
    repositories {
        mavenLocal()
    }
    dependencies {
        classpath 'com.gdlinkjob.bugly.plugin:betauploader:1.0.6'
    }
}


apply plugin: 'com.android.application'
apply plugin: 'groovyx.grooid.groovy-android'
apply plugin: 'com.tencent.bugly.plugin.betauploader' // 应用上传apk插件

// 内测分发 Gradle 插件使用指南
// https://bugly.qq.com/docs/utility-tools/plugin-gradle/
afterEvaluate {
    android.applicationVariants.all { variant ->
        def buglyAppId = null
        def buglyAppKey = null
        switch (variant.name) {
            case 'app1Release':
                buglyAppId = '900037672'
                buglyAppKey = 'bQvYLRrBNiqUctfi'
                break
            case 'app2Release':
                buglyAppId = '900036881'
                buglyAppKey = 'WROW4ybyWdWk59Qd'
                break
        }
        variant.ext.buglyAppId = buglyAppId
        variant.ext.buglyAppKey = buglyAppKey
    }
}

// beta配置
beta {
    title = "测试"
    desc = "你没事吧"
    enable = true
    autoUpload = true
    debugOn = true
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.devilwwj.plugintest"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        release {
            storeFile file("buglydemo.jks")
            storePassword "buglydemo"
            keyAlias "buglydemo"
            keyPassword "buglydemo"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "customer"

    productFlavors {
        app1 {
            dimension "customer"
        }

        app2 {
            dimension "customer"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:28.0.0'
}
